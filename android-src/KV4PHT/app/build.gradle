plugins {
    id 'com.android.application'
}

android {
    namespace 'com.vagell.kv4pht'
    compileSdk 35

    defaultConfig {
        applicationId "com.vagell.kv4pht"
        minSdk 26
        targetSdk 35
        versionCode 44
        versionName "1.9.4"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }

        // --- Git metadata injection ---
        def safeGit = { cmd ->
            try {
                return cmd.execute().text.trim()
            } catch (Exception e) {
                return "unknown"
            }
        }

        def gitCommitId = safeGit('git rev-parse --short HEAD')
        def gitBranch = safeGit('git rev-parse --abbrev-ref HEAD')
        def gitTag = safeGit('git describe --tags --abbrev=0')
        def gitDate = safeGit('git log -1 --format=%cd --date=short')
        def gitDirty = safeGit('git status --porcelain') ? 'true' : 'false'
        def buildTime = new Date().format("yyyy-MM-dd HH:mm:ss")

        buildConfigField "String", "GIT_COMMIT_ID", "\"${gitCommitId}\""
        buildConfigField "String", "GIT_BRANCH", "\"${gitBranch}\""
        buildConfigField "String", "GIT_TAG", "\"${gitTag}\""
        buildConfigField "String", "GIT_COMMIT_DATE", "\"${gitDate}\""
        buildConfigField "String", "BUILD_TIME", "\"${buildTime}\""
        buildConfigField "boolean", "GIT_DIRTY", "${gitDirty}"
        // -------------------------------
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildFeatures {
        viewBinding true
        dataBinding true
        buildConfig true
    }
    packagingOptions {
        resources.excludes.add("META-INF/*")
    }
}

dependencies {
    implementation project(':usbSerialForAndroid')
    implementation 'androidx.activity:activity:1.10.1'
    implementation 'org.junit.jupiter:junit-jupiter:5.8.1'
    implementation fileTree(dir: 'libs', include: ['*.aar', '*.jar'], exclude: [])
    implementation 'androidx.navigation:navigation-fragment:2.9.1'
    implementation 'androidx.navigation:navigation-ui:2.9.1'
    def room_version = "2.7.2"
    implementation "androidx.room:room-runtime:$room_version"
    annotationProcessor "androidx.room:room-compiler:$room_version"

    implementation 'androidx.appcompat:appcompat:1.7.1'
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.1'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.lifecycle:lifecycle-common-java8:2.9.1'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'
    implementation 'org.apache.commons:commons-math3:3.6.1'
    implementation 'org.apache.commons:commons-lang3:3.18.0'
    implementation 'com.google.zxing:core:3.4.1'

    implementation(platform("org.jetbrains.kotlin:kotlin-bom:1.8.22"))

    implementation 'com.google.android.gms:play-services-location:21.3.0'
    implementation 'org.projectlombok:lombok:1.18.30'
    annotationProcessor 'org.projectlombok:lombok:1.18.30'
    implementation 'io.github.jaredmdobson:concentus:1.0.1'
    implementation 'io.github.dkaukov:esp32-flash-lib:1.1.16'
    implementation 'dev.bright.slf4android:slf4android:0.1.8'
}